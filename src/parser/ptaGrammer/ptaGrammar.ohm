PTA {
	Program = Block*
	Block = Transaction | Recurring | RecEvent
   
    Transaction = Date Description Entry+
    
    Recurring = Date REPEAT RepeatClause Description Entry+
    
    RecEvent = Date RecEventKey Description
    RecEventKey = CREATED | ENDED
    
    RepeatClause = RepeatInterval? RepeatFrequency RepeatEnd?
    RepeatInterval = digit
    RepeatFrequency = DAY | WEEK | MONTH | YEAR
    RepeatEnd = Date
    
    Entry = Account price?
    
    price = amount currency
    
    amount = sign? mainAmount decimalAmount?
    sign = "-"
    mainAmount = digit+
    decimalAmount = decimal digit digit
    
    currency = letter letter letter
    
    Account = mainAccount subAccount*
    
    mainAccount = letter+
    subAccount = ":" letter+
    
    Description = delimiter descriptionBody delimiter
    descriptionBody = (~delimiter any)*
    
    Date =  year dash month dash day
    year = digit digit digit digit
    month = digit digit
    day = digit digit
    
    REPEAT = "repeat"
    CREATED = "created"
    ENDED = "ended"
    
    DAY = "day"
    WEEK = "week"
    MONTH = "month"
    YEAR = "year"
    
    entryDelimiter = newline whitespace
    delimiter = "\""
    whitespace = " " | "\t"
    dash = "-"
    newline = "\n"
    decimal = "."
    comment = "<" (~">" any)* ">"
    
    space += comment
}